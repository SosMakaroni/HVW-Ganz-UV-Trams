// --------------------------------------------------------------------------------UV11_01

// ss=spriteset, sg=spritegroup, sw=switch
// tmpl_1(szin,csuklo,rakodas,poziciok)
// tmpl_depo(szin,csuklo)

spriteset(ss_UV11_purchase, "src/UV1_BUY_8bpp.png")		{tmpl_purchase()}
spriteset(ss_UV11_PDO_purchase, "src/UV1_PDO_8bpp.png")		{tmpl_purchase()}
spriteset(ss_UV11_01, "src/UV1_01_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV11_01, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV1_01_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV11_01, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV1_01_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV11_01_PUP, "src/UV1_01_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV11_01_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV1_01_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV11_01_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV1_01_PUP_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_UV11_01 {	loaded:  [ss_UV11_01];	loading: [ss_UV11_01];}

switch (FEAT_ROADVEHS, SELF, swg_UV11_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_UV11_purchase;    // Alap modell
    1: return ss_UV11_PDO_purchase;    // Pantonograf (le)
}
switch (FEAT_ROADVEHS, SELF, swg_UV11_01, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_UV11_01;    // Alap modell
    1: return ss_UV11_01_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw_UV11_01, position_in_consist ) {
    1: ss_toldat;
    swg_UV11_01;
}
switch (FEAT_ROADVEHS, SELF, sw1_UV11_01_ss, cargo_subtype) {
	0: sw_UV11_01;
	CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_UV11_01_articulated, extra_callback_info1) {
    0..1: item_UV11_01;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_ROADVEHS, SELF, sw_UV11_01_capacity, position_in_consist) {
	1: return 0;
	return 100;
}
switch (FEAT_ROADVEHS, SELF, sw_UV11_01_length, position_in_consist) {
	1: return 8;
    return 7;
}
switch (FEAT_ROADVEHS, SELF, sw_UV11_01_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_BUDAPEST);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_UV11_01) {
	property {
        name:								string(STR_UV11_01_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1980,02,01);
        model_life:						20;
        vehicle_life:						50;
        reliability_decay:				10;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					39; //3*13
        cost_factor:						35;
        running_cost_factor:				40;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								65 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								180 kW;
        weight:							19 ton;
        cargo_capacity:					100;
		cargo_age_period:					30;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw1_UV11_01_ss;
		purchase:						swg_UV11_purchase;
		articulated_part:        		sw_UV11_01_articulated;
		length:                  		sw_UV11_01_length;
		cargo_subtype_text: 			sw_UV11_01_livery_text;
		cargo_capacity: 				sw_UV11_01_capacity;
		purchase_cargo_capacity:		return 50;
		cost_factor:					30 * parapuco;
		running_cost_factor:			30 * pararuco;
		additional_text: 				string(STR_UV11_01_ADAT);
    }
}
// --------------------------------------------------------------------------------UV11_01 End
