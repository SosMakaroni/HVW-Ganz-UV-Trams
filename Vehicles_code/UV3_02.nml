// --------------------------------------------------------------------------------UV32_02

// ss=spriteset, sg=spritegroup, sw=switch
// tmpl_1(szin,csuklo,rakodas,poziciok)
// tmpl_depo(szin,csuklo)

spriteset(ss_UV32_purchase, "src/UV32_BUY_8bpp.png")		{tmpl_purchase()}
spriteset(ss_UV32_PDO_purchase, "src/UV32_BUY_P_8bpp.png")		{tmpl_purchase()}
spriteset(ss_UV32_01, "src/UV3_01_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV32_01, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV3_01_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV32_01, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV3_01_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV32_01_PUP, "src/UV3_01_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV32_01_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV3_01_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV32_01_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV3_01_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV32_02, "src/UV3_02_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV32_02, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV3_02_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV32_02, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV3_02_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV32_02_PDO, "src/UV3_02_PDO_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV32_02_PDO, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV3_02_PDO_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV32_02_PDO, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV3_02_PDO_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV32pepsi_01, "src/UV3pepsi_01_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV32pepsi_01, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV3pepsi_01_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV32pepsi_01, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV3pepsi_01_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV32pepsi_01_PUP, "src/UV3pepsi_01_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV32pepsi_01_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV3pepsi_01_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV32pepsi_01_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV3pepsi_01_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV32pepsi_02, "src/UV3pepsi_02_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV32pepsi_02, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV3pepsi_02_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV32pepsi_02, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV3pepsi_02_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV32pepsi_02_PDO, "src/UV3pepsi_02_PDO_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV32pepsi_02_PDO, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV3pepsi_02_PDO_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV32pepsi_02_PDO, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV3pepsi_02_PDO_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_UV32_01 {	loaded:  [ss_UV32_01];	loading: [ss_UV32_01];}
spritegroup sg_UV32_02 {	loaded:  [ss_UV32_02];	loading: [ss_UV32_02];}
spritegroup sg_UV32pepsi_01 {	loaded:  [ss_UV32pepsi_01];	loading: [ss_UV32pepsi_01];}
spritegroup sg_UV32pepsi_02 {	loaded:  [ss_UV32pepsi_02];	loading: [ss_UV32pepsi_02];}

switch (FEAT_ROADVEHS, SELF, swg_UV32_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_UV32_purchase;    // Alap modell
    1: return ss_UV32_PDO_purchase;    // Pantonograf (le)
}
switch (FEAT_ROADVEHS, SELF, swg_UV32_01, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_UV32_01;    // Alap modell
    1: return ss_UV32_01_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, swg_UV32_02, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_UV32_02;    // Alap modell
    1: return ss_UV32_02_PDO;    // Pantonograf (le)
}
switch (FEAT_ROADVEHS, SELF, swg_UV32pepsi_01, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_UV32pepsi_01;    // Alap modell
    1: return ss_UV32pepsi_01_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, swg_UV32pepsi_02, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_UV32pepsi_02;    // Alap modell
    1: return ss_UV32pepsi_02_PDO;    // Pantonograf (le)
}

switch (FEAT_ROADVEHS, SELF, sw_UV32_02, position_in_consist ) {
    1: ss_toldat;
	2: swg_UV32_02;
	3: ss_toldat;
    swg_UV32_01;
}
switch (FEAT_ROADVEHS, SELF, sw_UV32pepsi_02, position_in_consist ) {
    1: ss_toldat;
	2: swg_UV32pepsi_02;
	3: ss_toldat;
    swg_UV32pepsi_01;
}
switch (FEAT_ROADVEHS, SELF, sw1_UV32_02_ss, cargo_subtype) {
	0: sw_UV32_02;
	1: sw_UV32pepsi_02;
	CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_UV32_02_articulated, extra_callback_info1) {
    0..3: item_UV32_02;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_ROADVEHS, SELF, sw_UV32_02_capacity, position_in_consist) {
	1: return 0;
	2: return 100;
	3: return 0;
	return 100;
}
switch (FEAT_ROADVEHS, SELF, sw_UV32_02_length, position_in_consist) {
	1: return 4;
	2: return 5;
	3: return 8;
    return 8;
}
switch (FEAT_ROADVEHS, SELF, sw_UV32_02_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_BUDAPEST);
	1: return string(STR_COLOUR_SCHEME_PEPSI);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_UV32_02) {
	property {
        name:								string(STR_UV32_02_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1980,02,01);
        model_life:						20;
        vehicle_life:						50;
        reliability_decay:				10;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					78; //6*13
        cost_factor:						70;
        running_cost_factor:				78;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								65 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								360 kW;
        weight:							38 ton;
        cargo_capacity:					200;
		cargo_age_period:					30;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw1_UV32_02_ss;
		purchase:						swg_UV32_purchase;
		articulated_part:        		sw_UV32_02_articulated;
		length:                  		sw_UV32_02_length;
		cargo_subtype_text: 			sw_UV32_02_livery_text;
		cargo_capacity: 				sw_UV32_02_capacity;
		purchase_cargo_capacity:		return 50;
		cost_factor:					62 * parapuco;
		running_cost_factor:			57 * pararuco;
		additional_text: 				string(STR_UV32_02_ADAT);
    }
}
// --------------------------------------------------------------------------------UV32_02 End
